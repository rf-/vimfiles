" Enable true color and special cursor for insert mode
if $TERM_PROGRAM == "iTerm.app"
  set termguicolors
endif
set guicursor=i:ver1,a:blinkon0

" Use cool popup blending
set pumblend=20
set winblend=10

" Use popup menu for command-line completion
set wildoptions=pum

" See instant feedback when entering commands
set inccommand=nosplit

" Quickly open a Ruby REPL
nnoremap <Leader>e :bel 10split term://pry<CR>

" Run selection in a Ruby REPL
vnoremap <Leader>e y:bel 10split term://pry<CR><C-\><C-n>pi<CR>

" Re-implementation of K with coc.nvim compatibility, taken from docs
function! ShowDocumentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

" Set up mappings for ALE and coc.nvim
function! s:set_up_coc_bindings()
  nmap <buffer> <silent> <Leader>k :CocAction<CR>
  vmap <buffer> <silent> <Leader>k <Plug>(coc-codeaction-selected)
  nmap <buffer> <silent> <C-p> :call ShowDocumentation()<CR>
  nmap <buffer> <silent> <C-]> <Plug>(coc-definition)
  nmap <buffer> <silent> <Leader>gtd <Plug>(coc-type-definition)
  nmap <buffer> <silent> <Leader>gi <Plug>(coc-implementation)
  nmap <buffer> <silent> <Leader>gr <Plug>(coc-references)
  nmap <buffer> <silent> <Leader>cr <Plug>(coc-rename)
  nmap <buffer> <silent> [d <Plug>(coc-diagnostic-prev)
  nmap <buffer> <silent> ]d <Plug>(coc-diagnostic-next)
  nmap <buffer> <silent> K :call ShowDocumentation()<CR>
endfunction
autocmd FileType javascript,typescript,typescriptreact,ruby call s:set_up_coc_bindings()

""" Terminal-related config

" Fix meta bindings in terminal
for c in range(0, 25)
  let char=nr2char(c + char2nr("a"))
  exec "tnoremap <M-" . char . "> <Esc>" . char

  let char=nr2char(c + char2nr("A"))
  exec "tnoremap <M-" . char . "> <Esc>" . char
endfor

" Bind <C-\><C-[> and <C-\><C-]> to switch tabs in all modes
tnoremap <C-\><C-[> <C-\><C-n>gT
tnoremap <C-\><C-]> <C-\><C-n>gt
nnoremap <C-\><C-[> gT
nnoremap <C-\><C-]> gt
vnoremap <C-\><C-[> gT
vnoremap <C-\><C-]> gt
inoremap <C-\><C-[> <C-o>gT
inoremap <C-\><C-]> <C-o>gt

" Bind <C-\><C-t> to open a new terminal split in normal and terminal modes
tnoremap <C-\><C-t> <C-\><C-n>:split<CR><C-\><C-n>:term<CR>
nnoremap <C-\><C-t> <C-\><C-n>:split<CR><C-\><C-n>:term<CR>

" Bind <C-\><C-p> to paste for convenience
tnoremap <C-\><C-p> <C-\><C-n>p

" Bind <C-\><C-\> to exit terminal
tnoremap <C-\><C-\> <C-\><C-n>

" Bind <M-k> to <C-k> for line editing
tnoremap <M-k> <C-k>

" Increase scrollback
let g:terminal_scrollback_buffer_size=100000

" Allow split navigation directly from terminal
tnoremap <C-h> <C-\><C-n><C-w>h
tnoremap <C-j> <C-\><C-n><C-w>j
tnoremap <C-k> <C-\><C-n><C-w>k
tnoremap <C-l> <C-\><C-n><C-w>l

" On entering a terminal buffer, automatically enter insert mode unless we're
" scrolled up
function! s:start_insert_if_at_bottom()
  if (line('w$') >= line('$'))
    startinsert
  endif
endfunction
autocmd TermOpen,BufWinEnter,WinEnter term://* call s:start_insert_if_at_bottom()

" When opening a term, disable line numbers; when leaving one, enable them
autocmd TermOpen,BufWinEnter term://* setlocal nonumber
autocmd BufWinLeave term://* setlocal number

" In terminal buffers, make <Enter> automatically jump into insert mode
nnoremap <expr> <Enter> &buftype == 'terminal' ? 'i<CR>' : '<CR>'
